zstyle ':completion:*' completer _complete _ignored _correct _approximate _prefix
zstyle ':completion:*' expand prefix suffix
zstyle ':completion:*' file-sort name
zstyle ':completion:*' group-name ''
zstyle ':completion:*' ignore-parents parent pwd
zstyle ':completion:*' insert-unambiguous false
zstyle ':completion:*' list-colors ''
zstyle ':completion:*' list-suffixes true
zstyle ':completion:*' matcher-list '+m:{[:lower:][:upper:]}={[:upper:][:lower:]}' '+r:|[._-]=** r:|=**'
zstyle ':completion:*' max-errors 3
zstyle ':completion:*' menu select=1
zstyle ':completion:*' original false
zstyle ':completion:*' preserve-prefix '//[^/]##/'
zstyle ':completion:*' verbose true

autoload -Uz compinit
compinit

HISTFILE=~/.histfile
HISTSIZE=1000
SAVEHIST=1000
setopt notify
unsetopt autocd beep extendedglob nomatch
bindkey -v

stty start undef
stty stop undef
stty lnext undef

path=("$HOME/zukunftslosigkeit/scripts" $path)
export PATH

export ALL_SHELLDEFS_PATH="$HOME/zukunftslosigkeit/shells"
export NIX_PATH="$NIX_PATH:$ALL_SHELLDEFS_PATH"

export PAGER='less --mouse'

# see also https://grml.org/zsh/zsh-lovers.html
rationalise-dot() {
  if [[ $LBUFFER = *.. ]]; then
    LBUFFER+=/..
  else
    LBUFFER+=.
  fi
}
zle -N rationalise-dot
bindkey . rationalise-dot

alias evcxr='evcxr --edit-mode vi'
alias nv='neovide --multigrid --noidle'
alias wh='wormhole-rs'
alias sqlite='sqlite3 -box'
alias rsync='rsync --progress'

alias cr='cargo run'
alias crr='cargo run --release'
alias cb='cargo build'
alias cbr='cargo build --release'
alias ch='cargo check'
alias cl='cargo clippy'
alias clp='cargo clippy -- -Wclippy::pedantic'
alias cw='cargo watch -c'
alias cf='cargo fmt'
alias ct='cargo nextest'
alias docs='cargo doc --open'
alias cm='cargo miri'
alias ca='cargo apk'
alias cfg='CARGO_PROFILE_RELEASE_DEBUG=true cargo flamegraph --palette aqua'

man() {
  if [[ ! -n $1 ]]; then
	echo "which manual page do you want?"
	return 1
  fi

  nv -- -c "Man $*" -c only
}

bydir() {
	dir="$(basename $(pwd))"
	touch $dir$1
}

# wrapper for nix-shell to make it launch zsh and cause less friction in my workflow
nix-shell() {
	SHELL_NAME="${SHELL_NAME:+$SHELL_NAME/}$@" \
		command nix-shell $@ \
			--command "SHELL=$(command which zsh) exec $(command which zsh) -i"
}
shell() {
	inner_cmd="SHELL=$(command which zsh) "
	if [[ -z $1 ]]; then
		# no args given and nothing to do by "default"
		return
	fi

	if [[ ! -d "$ALL_SHELLDEFS_PATH/$1" ]]; then
		echo "shell definition path '$1' not found"
		return
	fi

	if [[ -z $2 ]]; then
		# finally launch zsh
		inner_cmd="SHELL=$(command which zsh) exec $(command which zsh) -i"
	else
		# recurse further to handle rest of the given shells
		inner_cmd="exec $(command which zsh) -ic \"$funcstack[1] $argv[2,-1]\""
	fi

	before=$(pwd)
	cd "$ALL_SHELLDEFS_PATH/$1"

	# no magic variable, just set here for the prompt below
	# **do note this doesn't know of standalone nix-shells**
	SHELL_NAME="${SHELL_NAME:+$SHELL_NAME/}<$1>" \
		command nix-shell --command "cd $before; $inner_cmd"

	cd $before
}

setopt prompt_subst
RPROMPT=$RPROMPT'${SHELL_NAME:+ %2F\$${SHELL_NAME}%f}'

if [[ $HOST == "elusive" ]]; then
  eval "$(direnv hook zsh)"
fi
